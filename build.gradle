buildscript {
    ext {
        grailsVersion = project.grailsVersion
        apacheShiroVersion = "2.0.1"
    }
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
//        maven { url "https://dl.bintray.com/jfrog/jfrog-jars" }
    }
    dependencies {
//        classpath "com.vanniktech:gradle-dependency-graph-generator-plugin:0.8.0"
//        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4"
        classpath "org.grails:grails-gradle-plugin:$grailsGradlePluginVersion"
        classpath "org.asciidoctor:asciidoctor-gradle-plugin:1.6.1"
    }
}

version "4.5"
group "org.grails.plugins"

apply plugin: "eclipse"
apply plugin: "idea"
apply plugin: "org.grails.grails-plugin"
//apply plugin: "org.grails.grails-plugin-publish"
apply plugin: "org.grails.grails-gsp"
apply plugin: 'org.asciidoctor.convert'
apply plugin:"maven-publish"
//apply plugin: "com.vanniktech.dependency.graph.generator"

repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
//    maven { url "https://dl.bintray.com/jfrog/jfrog-jars" }
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

dependencies {
    developmentOnly("org.springframework.boot:spring-boot-devtools")
    compileOnly "io.micronaut:micronaut-inject-groovy"
    console "org.grails:grails-console"
    implementation "org.springframework.boot:spring-boot-starter-logging"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-autoconfigure"
    implementation "org.grails:grails-core"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-tomcat"
    implementation "org.grails:grails-web-boot"
    implementation "org.grails:grails-logging"
    implementation "org.grails:grails-plugin-rest"
    implementation "org.grails:grails-plugin-databinding"
    implementation "org.grails:grails-plugin-i18n"
    implementation "org.grails:grails-plugin-services"
    implementation "org.grails:grails-plugin-url-mappings"
    implementation "org.grails:grails-plugin-interceptors"
    implementation "org.grails.plugins:cache"
    implementation "org.grails.plugins:async"
    implementation "org.grails.plugins:scaffolding"
    implementation "org.grails.plugins:gsp"
    profile "org.grails.profiles:web-plugin"
    runtimeOnly "com.bertramlabs.plugins:asset-pipeline-grails:3.3.4"
    testImplementation "io.micronaut:micronaut-inject-groovy"
    testImplementation "org.grails:grails-gorm-testing-support"
    testImplementation "org.mockito:mockito-core"
    testImplementation "org.grails:grails-web-testing-support"


    implementation 'org.mindrot:jbcrypt:0.4'

    ['ehcache', 'core', 'spring', 'web', 'crypto-cipher'].each { pkg ->
        implementation("org.apache.shiro:shiro-$pkg:$apacheShiroVersion") {
            exclude module: 'ejb'
            exclude module: 'jsf-api'
            exclude module: 'servlet-api'
            exclude module: 'jsp-api'
            exclude module: 'jstl'
            exclude module: 'jms'
            exclude module: 'connector-api'
            exclude module: 'ehcache-core'
            exclude module: 'slf4j-api'
            exclude module: 'commons-logging'
        }
    }
}

bootRun {
    jvmArgs('-Dspring.output.ansi.enabled=always')
    sourceResources sourceSets.main
    String springProfilesActive = 'spring.profiles.active'
    systemProperty springProfilesActive, System.getProperty(springProfilesActive)
}

// enable if you wish to package this plugin as a standalone application
bootJar.enabled = false

//grailsPublish {
//    user = System.getenv('BINTRAY_USER')
//    key = System.getenv('BINTRAY_KEY')
//    userOrg = 'nerderg'
//    repo = 'plugins'
//    githubSlug = 'nerdErg/grails-shiro'
//    license {
//        name = 'Apache-2.0'
//    }
//    websiteUrl = "http://nerderg.com/Grails+shiro"
//    title = "Grails shrio plugin"
//    desc = "Secure your Grails application quickly and easily using the Apache Shiro security framework."
//    developers = [pmcneil: "Peter McNeil", pledbrook: "Peter Ledbrook", yellowsnow: "yellowsnow"]
//}

task shiroTest(type: Test) {
    dependsOn "install"
    //clean up anything left behind
    doFirst {
        delete "$projectDir/src/test/app"
    }

    doLast {
        mkdir "$projectDir/src/test/app"
        exec {
            workingDir "$projectDir/src/test/app"
            commandLine 'grails', 'create-app', 'shiro-tester'
        }

        def fixtures = "$projectDir/src/test/fixtures"
        def grailsApp = "$projectDir/src/test/app/shiro-tester/grails-app"
        def testSrc = "$projectDir/src/test/app/shiro-tester/src/"

        copy {
            from file("$fixtures/BootStrap.groovy")
            into file("$grailsApp/init/shiro/tester")
        }
        copy {
            from "$fixtures/controllers"
            into "$grailsApp/controllers"
        }
        copy {
            from "$fixtures/domain"
            into "$grailsApp/domain"
        }
        copy {
            from "$fixtures/services"
            into "$grailsApp/services"
        }
        copy {
            from "$fixtures/tests/shiro-tester/integration-test"
            into "$testSrc/integration-test"
        }

        //add the plugin
        File build = new File("$projectDir/src/test/app/shiro-tester/build.gradle")
        String content = build.text.replaceFirst('\ndependencies \\{', """dependencies {
    compile "org.grails.plugins:grails-shiro:4.3" 
    testCompile "net.sourceforge.htmlunit:htmlunit:2.32"
    testRuntime "org.seleniumhq.selenium:htmlunit-driver:2.32.1"
""")
        content = content.replaceAll(/repositories \{/, '''repositories {
mavenLocal()''')
        build.write(content)
        //add LDAP config

        new File("$grailsApp/conf/application.yml").append('''
---
server:
    servlet:
        context-path: /rainbow
---
security:
    shiro:
        rememberMe:
            cipherKey: 'abcdedfhijklmnopqrstuvwx'
        realm:
            ldap:
                server:
                    urls: ldap://localhost:10389
                search:
                    base: ou=users,dc=example,dc=com
                    user: uid=admin,ou=system
                    pass: secret
                    group:
                        name: ou=groups,dc=example,dc=com
                        member:
                            element: uniqueMember
                            prefix: uid=
                    permission:
                        commonName: cn=permissions
                        member:
                            element: uniqueMember
                            prefix: uid=
                username:
                    attribute: uid''')

        File settings = new File("$projectDir/src/test/app/shiro-tester/settings.gradle")
        settings.write("rootProject.name = 'grails-shiro-tester'")

        exec {
            workingDir "$projectDir/src/test/app/shiro-tester"
            commandLine 'grails', 'shiro-quick-start', '--package=com.nerderg.security'
        }
        exec {
            workingDir "$projectDir/src/test/app/shiro-tester"
            commandLine 'grails', 'generate-all', 'com.nerderg.Book'
        }
        exec {
            workingDir "$projectDir/src/test/app/shiro-tester"
            commandLine 'grails', 'create-shiro-controller-interceptor', 'com.nerderg.Book'
        }
        exec {
            workingDir "$projectDir/src/test/app/shiro-tester"
            commandLine 'grails', 'create-ldap-realm', '--package=com.nerderg.security'
        }

        //delete default failing tests
        delete "$testSrc/integration-test/groovy/com"
        delete "$testSrc/test/groovy/com"

        exec {
            workingDir "$projectDir/src/test/app/shiro-tester"
            commandLine './gradlew', 'integrationTest'
        }

        copy {
            from file("$projectDir/src/test/app/shiro-tester/build/reports/tests")
            into file("$projectDir/build/reports/tests/integrationtests")
        }

        delete "$projectDir/src/test/app"
    }
}

task shiroCliTest(type: Test) {
    dependsOn "install"
    //clean up anything left behind
    doFirst {
        delete "$projectDir/src/test/app"
    }

    doLast {
        mkdir "$projectDir/src/test/app"
        exec {
            workingDir "$projectDir/src/test/app"
            commandLine 'grails', 'create-app', 'cli-tester'
        }

        def fixtures = "$projectDir/src/test/fixtures"
        def testSrc = "$projectDir/src/test/app/cli-tester/src/"

        copy {
            from file("$fixtures/tests/cli-tester/unit-test")
            into file("$testSrc/test")
        }

        File build = new File("$projectDir/src/test/app/cli-tester/build.gradle")
        String content = build.text.replaceFirst('\ndependencies \\{', """dependencies {
    compile "org.grails.plugins:grails-shiro:4.3" """)
        content = content.replaceAll(/repositories \{/, '''repositories {
mavenLocal()''')
        build.write(content)

        File settings = new File("$projectDir/src/test/app/cli-tester/settings.gradle")
        settings.write("rootProject.name = 'grails-shiro-cli-tester'")

        exec {
            workingDir "$projectDir/src/test/app/cli-tester"
            commandLine './gradlew', 'test'
        }

        copy {
            from file("$projectDir/src/test/app/cli-tester/build/reports/tests")
            into file("$projectDir/build/reports/tests/clitests")
        }

        delete "$projectDir/src/test/app"

    }
}

//This needs to be last to work as expected.
tasks.withType(Test) {
//    ensure the LDAP server is running
    doFirst {
        println "Starting LDAP server"
        copy {
            from zipTree("src/test/fixtures/bin/apacheds-2.0.0.AM25.zip")
            into "src/test/fixtures/bin"
        }
        exec {
            workingDir projectDir
            commandLine 'src/test/fixtures/bin/apacheds-2.0.0.AM25/bin/apacheds.sh', 'start'
        }
        sleep(10000) //wait for the server to start on slower machines
    }

    doLast {
        println "Stoping LDAP server"
        exec {
            workingDir projectDir
            commandLine 'src/test/fixtures/bin/apacheds-2.0.0.AM25/bin/apacheds.sh', 'stop'
        }
        delete "src/test/fixtures/bin/apacheds-2.0.0.AM25"
    }
}

asciidoctor {
    sourceDir = file("docs")
    sources {
        include "Guide.adoc"
    }

    resources {
        from("$sourceDir/resources/images")
        into './resources/images'
    }
    
    attributes 'source-highlighter': 'coderay',
            toc                 : '',
            idprefix            : '',
            idseparator         : '-'

    outputDir = file("build/docs")
}

// ./gradlew publishToMavenLocal
publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'grails-shiro'
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            from components.java
        }
    }
}
